% 
% feta-timesig.mf --  implement Time Signatures
% 
% source file of the Feta (Font-En-Tja) music font
% 
% (c) 1998 Mats Bengtsson <matsb@s3.kth.se>

fet_begingroup("timesig");

def draw_C =
	save hair, bulb_rad;
	hair# := .5 stafflinethickness#;
	bulb_rad# := (interline# - stafflinethickness#) / 4;
	define_pixels(hair, bulb_rad);

	x1r = x5;
	y1r = interline / 3;
	z2r = (0, interline);
	z3r = (-b, 0);
	z4r = (0, -interline);
	z5r = (w, -stafflinethickness);
	
	penpos1(1.5 stafflinethickness, 0);
	penpos2(stafflinethickness, 90);
	penpos3(3 stafflinethickness, 180);
	penpos4(stafflinethickness, -90);
	penpos5(hair, -5);
	
	draw_bulb(-1, z1l,  z1r, bulb_rad, .8);

	fill z1l{up} .. z2l{left} .. z3l{down} .. z4l{right} ..
	simple_serif(z5l, z5r, -90) .. 
	z4r{left} .. z3r{up} .. z2r{right} .. z1r -- cycle;
	
%	labels(1,2,3,4,5);
	penlabels(1,2,3,4,5);
enddef;

fet_beginchar ("4/4 meter", "C", "fourfourmeter")
	set_char_box(.8 interline#, .7 interline#, interline#, interline#);
	
	draw_C;
fet_endchar;

fet_beginchar ("4/4 meter", "C2", "allabreve")
	set_char_box(.8 interline#, .7 interline#, 1.4 interline#, 1.4 interline#);
	
	draw_C;
	draw_block((-stafflinethickness, -d), (0, h));
fet_endchar;



fet_endgroup("timesig");
